plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'back'
version = '0.0.1-SNAPSHOT'
description = 'FCZ'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // 1. Application Core (핵심 기능)
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // 2. Developer Tools (개발 편의 기능)
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.retry:spring-retry'
    implementation 'org.aspectj:aspectjweaver'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

    // 3. API Documentation (OpenAPI/Swagger)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    // 4. Database Drivers (런타임 드라이버)
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j:9.1.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.redisson:redisson-spring-boot-starter:3.35.0'

    // 5. Testing (테스트 전용)
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.testcontainers:junit-jupiter'
    testCompileOnly 'org.projectlombok:lombok:1.18.32'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'
    testImplementation 'org.assertj:assertj-core:3.26.3'

    // 6. Security (보안 관련)
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // 7. sms sdk (문자 발송)
    implementation 'com.solapi:sdk:1.0.3'

    // 8. actuator (모니터링)
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // 9. openai api (컨텐츠 필터링용)
    implementation 'com.openai:openai-java:4.3.0'

    // 10. OAuth (OAuth 로그인)
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // 11. Google Drive (구글 드라이브 백업 관련)
    implementation 'com.google.api-client:google-api-client:2.0.0'
    implementation 'com.google.auth:google-auth-library-oauth2-http:1.15.0'
    implementation 'com.google.oauth-client:google-oauth-client:1.34.1'
    implementation 'com.google.apis:google-api-services-drive:v3-rev20220815-2.0.0'
    implementation 'com.google.http-client:google-http-client-gson:1.42.3'

    // 12. s3
    implementation 'software.amazon.awssdk:s3:2.25.0'
    implementation 'software.amazon.awssdk:auth:2.25.0'
    implementation 'software.amazon.awssdk:regions:2.25.0'

    //13. resilience4j
    implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.0.2'
}

// CI 테스트 제외
tasks.withType(Test).configureEach {
    useJUnitPlatform {
        if (System.getenv("CI") == "true") {
            excludeTags("redis", "integration", "sms")
        }
    }
}

